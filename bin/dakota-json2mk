#!/usr/bin/perl -w
# -*- mode: cperl -*-
# -*- cperl-close-paren-offset: -2 -*-
# -*- cperl-continued-statement-offset: 2 -*-
# -*- cperl-indent-level: 2 -*-
# -*- cperl-indent-parens-as-block: t -*-
# -*- cperl-tab-always-indent: t -*-

use strict;
use warnings;

use Getopt::Long;
$Getopt::Long::ignorecase = 0;

use Data::Dumper;
$Data::Dumper::Terse =    1;
$Data::Dumper::Useqq =    1;
$Data::Dumper::Sortkeys = 1;

# gnu make variable assignment operators
# = or := or ::= or ?=
my $EQ = '=';
my $target_specific_var_tbl = {};

sub is_scalar {
  my ($v) = @_;
  my $result = 0;
  if ('' eq ref($v)) {
    $result = 1;
  }
  return $result;
}
sub is_tbl {
  my ($v) = @_;
  my $result = 0;
  if ('HASH' eq ref($v)) {
    $result = 1;
  }
  return $result;
}
sub is_seq {
  my ($v) = @_;
  my $result = 0;
  if ('ARRAY' eq ref($v)) {
    $result = 1;
  }
  return $result;
}
sub first_char {
  my ($str) = @_;
  return substr($str, 0, 1);
}
sub last_char {
  my ($str) = @_;
  return substr($str, -1, 1);
}
sub is_protected_name {
  my ($name) = @_;
  my $result = 0;
  if ('-' eq &first_char($name)) {
    $result = 1;
  }
  return $result;
}
sub lhs {
  my ($scopes, $prefix) = @_;
  my $result;

  if ($prefix) {
    $result = join('.', ($prefix, @$scopes));
  } else {
    $result = join('.', @$scopes);
  }
  $result = &convert($result);
  return $result;
}
sub prefix {
  my ($opts) = @_;
  my $prefix = $$opts{'prefix'};

  if ($prefix) {
    $prefix =~ s/\s+//g; # warn about whitespace in prefix?
    $prefix = undef if '' eq $prefix;
  }
  return $prefix;
}
sub convert {
  my ($str) = @_;
  my $result = $str;
  if (1) {
    my $trans_tbl = { '\-' => '_',
                      '\.' => '_', };
    while (($a, $b) = each (%$trans_tbl)) {
      $result =~ s/$a/$b/g;
    }
  }
  return $result;
}
sub is_valid {
  my ($str) = @_;
  my $result = 0;
  if ($str =~ /^\w+$/) {
    $result = 1;
  }
  return $result;
}
sub process_tbl {
  my ($data, $opts, $scopes, $out) = @_;

  foreach my $name (sort keys %$data) {
    if (&is_protected_name($name)) {
      next;
    } elsif (!&is_valid(&convert($name))) {
      print STDERR "$0: warning: skipping key $name\n";
      next;
    } else {
      push @$scopes, $name;
      $_ = ref($$data{$name});
      SWITCH: {
          /^HASH$/  and &process_tbl(        $$data{$name}, $opts, $scopes, $out), last SWITCH;
          /^ARRAY$/ and &process_seq_expr(   $$data{$name}, $opts, $scopes, $out), last SWITCH;
                        &process_scalar_expr($$data{$name}, $opts, $scopes, $out);
      }
      pop @$scopes;
    }
  }
}
# scalar is member of rhs seq
sub process_scalar {
  my ($data, $opts, $lhs, $out) = @_;
  push @{$$out{$lhs}}, $data;
}
# scalar is rhs of expr (lhs = rhs)
sub process_scalar_expr {
  my ($data, $opts, $scopes, $out) = @_;
  my $prefix = &prefix($opts);

  if ($prefix) {
    $$target_specific_var_tbl{$prefix}{&lhs($scopes, undef)} = &lhs($scopes, $prefix);
  }
  $$out{&lhs($scopes, $prefix)} = $data;
}
# seq is rhs of expr (lhs = rhs ...)
sub process_seq_expr {
  my ($data, $opts, $scopes, $out) = @_;
  my $prefix = &prefix($opts);

  if ($prefix) {
    $$target_specific_var_tbl{$prefix}{&lhs($scopes, undef)} = &lhs($scopes, $prefix);
  }
  my $lhs = &lhs($scopes, $prefix);
  $$out{$lhs} = [];

  foreach my $element (@$data) {
    if (&is_scalar($element)) {
      #push @{$$out{$lhs}}, $element;
      &process_scalar($element, $opts, $lhs, $out);
    }
  }
}
sub mkstr {
  my ($out, $opts) = @_;
  my $result = '';
  foreach my $lhs (sort keys %$out) {
    my $rhs = $$out{$lhs};
    $result .= $lhs . ' ' . $EQ;
    if (&is_scalar($rhs)) {
      $rhs = [ $rhs ];
    }
    foreach my $element (@$rhs) {
      $result .= ' ' . $element;
    }
    $result .= "\n";
  }
  return $result;
}
sub start {
  my ($argv) = @_;
  my $opts = {};
  my $scopes = [];
  &GetOptions($opts,
              'output=s',
              'prefix=s',
            );
  my $fh;

  if ($$opts{'output'}) {
    open($fh, ">", $$opts{'output'})
      or die "cannot open > $$opts{'output'}: $!";
  } else {
    open($fh, '>&', \*STDOUT)
      or die "cannot open >& STDOUT: $!";
  }
  foreach my $file (@$argv) {
    my $data = do $file or die;
    #print &Dumper($data);
    my $out = {};
    &process_tbl($data, $opts, $scopes, $out);
    print $fh
      "# generated from $file\n" .
      &mkstr($out);
    print $fh "\n" . &target_specific_vars($opts, $out);
  }
  close($fh);
}
sub target_specific_vars {
  my ($opts, $out) = @_;
  #print STDERR &Dumper($out);
  my $prefix = &prefix($opts);
  my $result = '';
  if ($prefix) {
    my $output = &lhs(['output'], $prefix);

    foreach my $bname (sort keys %{$$target_specific_var_tbl{$prefix}}) {
      my $name = $$target_specific_var_tbl{$prefix}{$bname};
      $result .= "\$($output)" . ' : ' . $bname . ' = ' . "\$($name)" . "\n";
    }
  }
  #print STDERR $result;
  return $result;
}
unless (caller) {
  &start(\@ARGV);
}
