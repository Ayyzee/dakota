#!/usr/bin/perl -w
# -*- mode: cperl -*-
# -*- cperl-close-paren-offset: -2 -*-
# -*- cperl-continued-statement-offset: 2 -*-
# -*- cperl-indent-level: 2 -*-
# -*- cperl-indent-parens-as-block: t -*-
# -*- cperl-tab-always-indent: t -*-

use strict;
use warnings;

use Getopt::Long;
$Getopt::Long::ignorecase = 0;

use Data::Dumper;
$Data::Dumper::Terse =    1;
$Data::Dumper::Useqq =    1;
$Data::Dumper::Sortkeys = 1;

# gnu make variable assignment operators
# = or := or ::= or ?=
my $EQ = '=';

sub is_scalar {
  my ($v) = @_;
  my $result = 0;
  if ('' eq ref($v)) {
    $result = 1;
  }
  return $result;
}
sub is_tbl {
  my ($v) = @_;
  my $result = 0;
  if ('HASH' eq ref($v)) {
    $result = 1;
  }
  return $result;
}
sub is_seq {
  my ($v) = @_;
  my $result = 0;
  if ('ARRAY' eq ref($v)) {
    $result = 1;
  }
  return $result;
}
sub first_char {
  my ($str) = @_;
  return substr($str, 0, 1);
}
sub last_char {
  my ($str) = @_;
  return substr($str, -1, 1);
}
sub is_protected_name {
  my ($name) = @_;
  my $result = 0;
  if ('-' eq &first_char($name)) {
    $result = 1;
  }
  return $result;
}
sub lhs {
  my ($scopes, $prefix) = @_;
  my $result;

  if ($prefix) {
    $result = join('.', ($prefix, @$scopes));
  } else {
    $result = join('.', @$scopes);
  }
  $result = &convert($result);
  return $result;
}
sub prefix {
  my ($opts) = @_;
  my $prefix = $$opts{'prefix'};

  if ($prefix) {
    $prefix =~ s/\s+//g; # warn about whitespace in prefix?
    $prefix = undef if '' eq $prefix;
  }
  return $prefix;
}
sub convert {
  my ($str) = @_;
  my $result = $str;
  if (1) {
    my $trans_tbl = {
      '\-' => '_',
      '\.' => '_',
      '\/' => '_',
      '\:' => '_',
    };
    while (($a, $b) = each (%$trans_tbl)) {
      $result =~ s|$a+|$b|g;
    }
  }
  return $result;
}
sub is_valid {
  my ($str) = @_;
  my $result = 0;
  if ($str =~ /^\w+$/) {
    $result = 1;
  }
  return $result;
}
sub process_tbl {
  my ($data, $opts, $scopes, $out) = @_;

  foreach my $name (sort keys %$data) {
    if (&is_protected_name($name)) {
      next;
    } elsif (!&is_valid(&convert($name))) {
      print STDERR "$0: warning: skipping key $name\n";
      next;
    } else {
      push @$scopes, $name;
      $_ = ref($$data{$name});
      SWITCH: {
          /^HASH$/  and &process_tbl(        $$data{$name}, $opts, $scopes, $out), last SWITCH;
          /^ARRAY$/ and &process_seq_expr(   $$data{$name}, $opts, $scopes, $out), last SWITCH;
                        &process_scalar_expr($$data{$name}, $opts, $scopes, $out);
      }
      pop @$scopes;
    }
  }
}
sub target_specific_vars_set {
  my ($opts, $scopes, $out) = @_;
  my $prefix = &prefix($opts);
  if ($prefix) {
    $$out{'tsv'}{$prefix}{&lhs($scopes, undef)} = &lhs($scopes, $prefix);
  }
  my $lhs = &lhs($scopes, $prefix);
  return $lhs;
}
# scalar is member of rhs seq
sub process_scalar {
  my ($data, $opts, $lhs, $out) = @_;
  push @{$$out{'mk'}{$lhs}}, $data;
}
# scalar is rhs of expr (lhs = rhs)
sub process_scalar_expr {
  my ($data, $opts, $scopes, $out) = @_;
  my $lhs = &target_specific_vars_set($opts, $scopes, $out);
  $$out{'mk'}{$lhs} = $data;
}
# seq is rhs of expr (lhs = rhs ...)
sub process_seq_expr {
  my ($data, $opts, $scopes, $out) = @_;
  my $lhs = &target_specific_vars_set($opts, $scopes, $out);
  $$out{'mk'}{$lhs} = [];

  foreach my $element (@$data) {
    if (&is_scalar($element)) {
      #push @{$$out{'mk'}{$lhs}}, $element;
      &process_scalar($element, $opts, $lhs, $out);
    }
  }
}
sub mkstr {
  my ($out, $opts) = @_;
  my $result = '';
  foreach my $lhs (sort keys %{$$out{'mk'}}) {
    my $rhs = $$out{'mk'}{$lhs};
    $result .= $lhs . ' ' . $EQ;
    if (&is_scalar($rhs)) {
      $rhs = [ $rhs ];
    }
    foreach my $element (@$rhs) {
      $result .= ' ' . $element;
    }
    $result .= "\n";
  }
  return $result;
}
sub start {
  my ($argv) = @_;
  my $opts = {};
  my $scopes = [];
  &GetOptions($opts,
              'output=s',
              'prefix=s',
            );
  my $fh;

  if ($$opts{'output'}) {
    open($fh, ">", $$opts{'output'})
      or die "cannot open > $$opts{'output'}: $!";
  } else {
    open($fh, '>&', \*STDOUT)
      or die "cannot open >& STDOUT: $!";
  }
  foreach my $file (@$argv) {
    my $data = do $file or die;
    #print &Dumper($data);
    my $out = { 'mk' => {}, 'tsv' => {}, };
    &process_tbl($data, $opts, $scopes, $out);
    #print &Dumper($out);
    print $fh
      "# generated from $file\n" .
      &mkstr($out);
    print $fh "\n" . &target_specific_vars($data, $opts, $out);
  }
  close($fh);
}
sub tsv_install{
  my ($data, $key, $prefix) = @_;
  my $result = '';
  #my $output = &lhs(['output'], $prefix);
  my $install_libs = $$data{'install'}{$key};

  for (my $i = 0; $i < @$install_libs; $i++) {
    my $install_lib = $$install_libs[$i];
    if (&is_scalar($install_lib)) {
      my $info = $$install_libs[$i + 1];
      if (&is_tbl($info)) {
        my ($bname, $name); while (($bname, $name) = each (%$info)) {
          $result .= $install_lib . ' : ' . $bname . ' = ' . $name . "\n";
          $i++;
        }
      }
    }
  }
  return $result;
}

sub target_specific_vars {
  my ($data, $opts, $out) = @_;
  #print STDERR &Dumper($out);
  my $prefix = &prefix($opts);
  my $result = '';
  if ($prefix) {
    my $install =  &lhs(['install'], $prefix);
    my $install_includes = &lhs(['install', 'includes'], $prefix);
    my $install_libs = &lhs(['install', 'libs'], $prefix);
    my $install_bins = &lhs(['install', 'bins'], $prefix);
    $result .= "$install" . ' = ' . "\$($install_includes)" . ' ' . "\$($install_libs)" . ' ' . "\$($install_bins)" . "\n";
    $result .= "\n";
    my $output = &lhs(['output'], $prefix);
    my $libs = &lhs(['libs'], $prefix);
    foreach my $bname (sort keys %{$$out{'tsv'}{$prefix}}) {
      my $name = $$out{'tsv'}{$prefix}{$bname};
      $result .= "\$($output)" . ' : ' . $bname . ' = ' . "\$($name)" . "\n";
    }
    $result .= "\n";
    my $srcs = &lhs(['srcs'], $prefix);
    $result .= "\$($output)" . ' : ' . "\$($srcs)" . ' ' . "\$($libs)" . "\n";

    my ($includes_ln, $libs_ln, $bins_ln);
    $includes_ln = &tsv_install($data, 'includes', $prefix);
    $libs_ln =     &tsv_install($data, 'libs',     $prefix);
    $bins_ln =     &tsv_install($data, 'bins',     $prefix);
    if ('' ne $includes_ln || '' ne $libs_ln || '' ne $bins_ln) {
      $result .= "\n" . $includes_ln . $libs_ln . $bins_ln;
    }
  }
  #print STDERR $result;
  return $result;
}
unless (caller) {
  &start(\@ARGV);
}
